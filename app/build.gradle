plugins {
    id "fr.coppernic.versioning" version "3.1.2"
}

apply plugin: 'com.android.application'
apply plugin: 'fr.coppernic.extensions'


copper {
    projectName = "PcscSample"

    android {
        flavorDimensions += "peripheral"
    }
    publications {
        artiConfig = project.hasProperty('artiServer') ? project.artiServer : "arti01"
    }
}

apply plugin: 'fr.coppernic.android'

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "fr.coppernic.samples.pcsc"
        minSdkVersion 27
        targetSdkVersion 28
        vectorDrawables.useSupportLibrary = true
        versionCode versioning.info.versionCode
        versionName versioning.info.display
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        ndk {
            abiFilters "armeabi", "armeabi-v7a"
        }

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
        exclude 'META-INF/lib_release.kotlin_module'
    }


    productFlavors {
        normal {
            dimension "peripheral"
//            versionNameSuffix ""
            manifestPlaceholders = [appLabel:"@string/app_name"]
            applicationId "fr.coppernic.samples.pcsc"
        }
        hiddev {
            dimension "peripheral"
            versionNameSuffix "-hiddev"
            manifestPlaceholders = [appLabel:"@string/app_name_hiddev"]
            applicationId "fr.coppernic.samples.pcsc.hid"
        }
        smartcard {
            dimension "peripheral"
            versionNameSuffix "-smartcard"
            manifestPlaceholders = [appLabel:"@string/app_name_smartcard"]
            applicationId "fr.coppernic.samples.pcsc.smartcard"
        }
    }

    sourceSets {
        normal {
            java {
                srcDir 'src/normal/java'
            }
        }
        hiddev {
            java {
                srcDir 'src/hiddev/java'
            }
        }
        smartcard {
            java {
                srcDir 'src/smartcard/java'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "com.google.android.material:material:1.5.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    //Canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'

    //Material Design
    implementation "androidx.preference:preference:1.2.0"
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"
    implementation 'com.weiwangcn.betterspinner:library-material:1.1.0'

    implementation 'com.github.d-max:spots-dialog:0.7@aar'

    // Coppernic
    implementation 'fr.coppernic.sdk.cpcutils:CpcUtilsLib:6.19.1'
    implementation 'fr.coppernic.sdk.core:CpcCore:2.1.12'
    implementation 'fr.coppernic.lib:splash:0.3.0'

    implementation 'fr.coppernic.sdk.pcsc2:pcsclib:master'

    implementation "androidx.cardview:cardview:1.0.0"
}

apply from: rootProject.file('gradle/publish-app.gradle')
